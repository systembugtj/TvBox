apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def repo_context_url = hasProperty('artifactory_context_url') ? artifactory_context_url : System.getenv('artifactory_context_url')
def repo_username = hasProperty('artifactory_username') ? artifactory_username : System.getenv('artifactory_username')
def repo_password = hasProperty('artifactory_password') ? artifactory_password : System.getenv('artifactory_password')
def repo_key = hasProperty('artifactory_key') ? artifactory_key : System.getenv('artifactory_key')

def VERSION_NAME = System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "1.1.0-SNAPSHOT"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    final SUPPOERT_VERSION = "23.4.0"

    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:${SUPPOERT_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPOERT_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPOERT_VERSION}"
    compile "com.android.support:support-v4:${SUPPOERT_VERSION}"

    compile 'com.google.guava:guava:19.0+'

    // Iconify
    compile 'com.joanzapata.iconify:android-iconify-material:2.2.2'
}

def libraryGroupId = 'me.systembug.tv'
def libraryArtifactId = 'tv-view'
def libraryVersion = VERSION_NAME

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId

            artifact("$buildDir/outputs/aar/${project.name}-release.aar")
        }
    }
}

artifactory {
    contextUrl = repo_context_url
    publish {
        repository {
            repoKey = repo_key

            username = repo_username
            password = repo_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}
